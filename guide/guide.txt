~theme HorizonDark
~title Guide to Wave Markup Language
~box 100

~set code_bg #282828
~set code_fg #98971A

!align center
!size 50
!color green
$text Wave

$nl 3

!size 25
!color red
$text A markup language that transpiles its syntax to HTML with inline CSS.

$nl 5

!align left
!size 20
!color fg
$text Wave is a minimalist markup language that transpiles to HTML + CSS. The transpiled document is viewed using a web browser.

$nl 3

!size 25
!color green
$text 1. Getting Wave

$nl
!size 20
!color fg
$text Wave is available for free on Github. Wave is available in source code form only.

$nl
$text Wave on Github:
!align center
$link https://www.github.com/KILLinefficiency/Wave ; https://www.github.com/KILLinefficiency/Wave

!align left

$nl 3
$text This documentation for Wave is written in Wave itself.

$nl 2
$text Before compiling Wave, make sure that you have the following things installed:
$nl
$points The Go Programming Language ; GNU Make ; A Web Browser

$nl 3
$text Clone Wave onto your system:

$nl
!font Consolas
!color blue
$text %sp%sp%sp%sp $ git clone https://www.github.com/KILLinefficiency/Wave.git
!color fg
!font Arial

$nl 2

$text Build Wave:
$nl

!font Consolas
!color blue
$text %sp%sp%sp%sp $ cd Wave
$text %sp%sp%sp%sp $ make
!color fg
!font Arial

$nl 2
$text An executable for Wave will be produced. You can use the executable as it is or add it to your PATH environment variable.

$nl 2
$text For a Non-Unix system like Windows, which does not support GNU Make natively, compile Wave directly.
$nl 2

!font Consolas
!color blue
$text %sp%sp%sp%sp > git clone https://www.github.com/KILLinefficiency/Wave.git
$text %sp%sp%sp%sp > cd Wave
$text %sp%sp%sp%sp > go build wave.go lib.go themes.go defaults.go variables.go contentLib.go htmlTemplates.go
!font Arial

$nl 3

!size 25
!color green
$text 2. Structure of a Wave script

!size 20
!color fg

$nl 2

$text A Wave script is written in a plain text file. Wave scripts do not follow any extension. You can have an extension for a Wave script or not as long as it's a text file. However, it's better to have a <i>.txt</i> extension.

$nl 2

!color green
$text 2.1 Compiling a Wave script

$nl 2

!color fg
$text A Wave script can be compiled by passing it to the Wave transpiler as a command-line argument.

$nl 2

!color blue
!font Consolas

$text %sp%sp%sp%sp $ wave guide.txt

!color fg
!font Arial

$nl 2
$text The transpiler generates a <i>.html</i> file of the same name. This is the final document.

$nl 2
$text A Wave script consists of three different types of keywords:

$nl

!color brown
!points-type ordered
!points-style lower-roman
$points The Page Property Keywords ; The Content Property Keywords ; The Content Keywords

$nl 2

!color green
$text 2.2 The Page Property Keywords

!color fg

$nl
$text The page property keywords start with the symbol <i>~</i>. These properties apply to the entire document. These properties include some important elements that define the look of the document.

$nl

$text Here's a table of the page properties and their uses:

$nl

!align center
!color brown

$table <b>Page Property</b> ; <b>Use</b> ; <b>Example</b> | ~title ; Sets the title to tab in Browser Window ; <i>~title Hello!</i> | ~theme ; Sets theme to the document ; <i>~theme HorizonDark</i> | ~bg ; Sets the background color of the document ; <i>~bg green</i> | ~fg ; Sets the default text color ; <i>~fg yellow</i> | ~img ; Sets an image as the document background ; <i>~img batman.png</i> | ~box ; Sets a padding specified in pixels to the document ; <i>~box 100</i> | ~box-style ; Sets a visible border to the padding (~box-style is same as CSS border-styles and uses the same values) ; <i>~box-style solid</i> | ~set ; Declares a variable and sets a value to it ; <i>~set name Bruce Wayne</i>

$nl 2

!align left
!color green

$text 2.3 The Content Property Keywords

$nl

!color fg
$text The content property keywords start with the symbol <i>!</i>. These properties apply to the entire content that comes after the declaration of the content property. These properties define the look of the content in the document.
$nl

$text Here's a table of the the content properties and their uses:

$nl

!align center
!color brown

$table <b>Content Property</b> ; <b>Use</b> ; <b>Example</b> | !bg ;  Sets background color to the content ; <i>!bg red</i> | !tab ; Changes the number of spaces in a tab; <i>!tab 8</i> | !font ; Changes the font ; <i>!font Consolas</i> | !size ; Changes the font size (specified in pixels) ; <i>!size 50</i> | !align ; Changes the alignment of the content ; <i>!align right</i> | !color ; Changes the foreground of the text ; <i>!color blue</i> | !points-type ; Sets the points to ordered (ol) or unordered (ul); <i>!points-type ol</i> | !points-style ; Sets a style to the points ; <i>!points-style lower-roman | !box ; Sets a padding specified in pixels around the content ; <i>!box 15</i> | !box-style ; Sets a visible border to the padding around the content ; <i>!box-style dotted</i> | !sep ; Sets the delimiter ; <i>!sep ,</i> | !colsep ; Sets the column delimiter ; <i>!colsep -</i> | !dim ; Sets the dimensions of a picture in form: width(px)xheight(px) ; <i>!dim 200x100</i> | !default ; Sets all the content properties to default ; <i>!default</i>

$nl 2

!align left
!color green

$text 2.4 The Content Keywords

$nl

!color fg

$nl

$text The content keywords start with the symbol <i>$</i>. These keywords define the contents of the document. Contents include text, pictures, tables, etc.

$nl

$text Here are all the content keywords explained:

$nl 2
!color purple
$text 2.4.1. %sp%sp%sp%sp $text
!color fg
$nl 2

$text As the name suggests, <i>$text</i> adds text in the document.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~bg green<br>!color yellow<br>$text Hello!
!font Arial
!box 0
!bg bg


$nl 2
!color purple
$text 2.4.2. %sp%sp%sp%sp $nl
!color fg
$nl 2

$text <i>$nl</i> adds new line(s) in the document. By default <i>$nl</i> adds only one new line, but more new lines can be added by specifying the number to <i>$nl</i>. Like, <i>$nl 5</i> for five new lines.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !align center<br>$text Hello World!<br>$nl 3<br>!align right<br>$text How are you?
!font Arial
!box 0
!bg bg


$nl 2
!color purple
$text 2.4.3. %sp%sp%sp%sp $link
!color fg
$nl 2

$text Insert a link in the document using <i>$link</i>.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $link https://www.github.com ; Github
!font Arial
!box 0
!bg bg
!color fg

$nl 2
$text The default delimiter (<i>;</i>) can be changed to any other character using the content property <i>!sep</i>.
$nl

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !sep ,<br>$link https://www.github.com , Github
!font Arial
!box 0
!bg bg


$nl 2
!color purple
$text 2.4.4. %sp%sp%sp%sp $mail
!color fg
$nl 2

$text Like <i>$link</i>, <i>$mail</i> allows you to add mail addresses in the document.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $mail oliverqueen@queenconsolidated.com ; Contact Oliver Queen
!font Arial
!box 0
!bg bg

!color fg
$text The default delimiter (<i>;</i>) can, of course, be changed with the content property <i>!sep</i>.
$nl 2


$nl 2
!color purple
$text 2.4.5. %sp%sp%sp%sp $file
!color fg
$nl 2

$text <i>$file</i> reads the contents of a file and adds them to the document.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $file some_random_file.txt
!font Arial
!box 0
!bg bg

!color fg
$nl
$text The file is read as it is in Wave. The default tab used while reading is 4 spaces. However, you can use the <i>!tab</i> content property to change this.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !tab 8<br>$file some_random_file.txt
!font Arial
!box 0
!bg bg

!color fg
$text This sets the tab to 8 spaces.
$nl 2


$nl 2
!color purple
$text 2.4.6. %sp%sp%sp%sp $quote
!color fg
$nl 2

$text Use <i>$quote</i> to add quotations in your document.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $quote %"Being happy doesn't mean you don't have issues. It just means that you're working on them.%"%<br%>%<br%>- Oliver Queen
!font Arial
!box 0
!bg bg


$nl 2
!color purple
$text 2.4.7. %sp%sp%sp%sp $pic
!color fg
$nl 2

$text Insert images into your document using <i>$pic</i>. Pass the absolute or relative path of the image to <i>$pic</i>.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $pic resources/images/batman.png
!font Arial
!box 0
!bg bg

!color fg
$text You can also specify the dimensions of the image using the <i>!dim</i> content property. Specify the width and the height of the image respectively in pixels, separated by the character <i>x</i>.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !dim 750x500<br>$pic resources/images/batman.png
!font Arial
!box 0
!bg bg

!color fg
$text You can also choose to set only the width of the image.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !dim 750x<br>$pic resources/images/batman.png
!font Arial
!box 0
!bg bg

!color fg
$text In the same way, you can also choose to set only the height of the image.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !dim x500<br>$pic resources/images/batman.png
!font Arial
!box 0
!bg bg

!color fg
$text Also, you can set the size of the image back to normal.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !dim x<br>$pic resources/images/batman.png
!font Arial
!box 0
!bg bg
!color fg


$nl 2
!color purple
$text 2.4.8. %sp%sp%sp%sp $points
!color fg
$nl 2

$text Add a list of points in your document using <i>$points</i>.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $text Bat Family:<br>$nl<br>$points Bruce Wayne ; Alfred Pennyworth ; Lucius Fox ; Dick Grayson ; Jason Todd ; Tim Drake ; Damain Wayne ; Barbara Gordon
!font Arial
!box 0
!bg bg

!color fg
$text The default delimiter (;) can be changed using the content property <i>!sep</i>.
$nl
$text You can also change between ordered and unordered lists and change their styles. Use the <i>!points-type</i> content property to switch between ordered list (<i>ol</i>) or unordered list (<i>ul</i>). Use the <i>!points-style</i> content property to change the style of the points.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !points-type ol<br>!points-style lower-roman<br>$points Oliver Queen ; Bruce Wayne<br><br>$nl 2<br>!point-type ul<br>!points-style square<br>$points Green Arrow ; Batman
!font Arial
!box 0
!bg bg
!color fg

$text The point styles used in Wave are the same as the list styles used in HTML.
$nl 2


$nl 2

!color purple
$text 2.4.9. %sp%sp%sp%sp $check
!color fg

$nl 2
$text Add checkboxes inside the document using <i>$check</i>.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $text Guest List:<br>$check Oliver Queen ; Barry Allen ; Kara Danvers ; Clark Kent ; Kate Kane ; Bruce Wayne
!font Arial
!box 0
!bg bg

!color fg
$text The default delimiter (<i>;</i>) can be changed using the content property <i>!sep</i>.
$nl 2


$nl 2
!color purple
$text 2.4.10. %sp%sp $table
!color fg

$nl 2
$text Add tables in your document using <i>$table</i>. The items for the rows are separated by the default delimiter (<i>;</i>) and the rows are separated by the default column separator (<i>|</i>).
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $table Name ; Alias | Oliver Queen ; Green Arrow | Barry Allen ; The Flash
!font Arial
!box 0
!bg bg

!color fg
$text Tables can be made with any number of rows and columns. Like,
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $table Name ; Alias ; Abilities ; City | Oliver Queen ; Green Arrow ; Archery and hand-to-hand combat ; Star City | Barry Allen ; The Flash ; Super Speed ; Central City | Clark Kent ; Superman ; Superhuman Strength ; Metropolis | Bruce Wayne ; Batman ; Detective skills and hand-to-hand combat ; Gotham
!font Arial
!box 0
!bg bg

!color fg
$text The item separator and the column separator can be changed using the <i>!sep</i> and the <i>!colsep</i> content properties respectively. Like,

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text !sep ,<br>!colsep -<br>$table Name , Alias - Oliver Queen , Green Arrow - Barry Allen , The Flash
!box 0
!bg bg
!color fg


$nl 2
!color purple
$text 2.4.11. %sp%sp $html
!color fg
$nl 2

$text Add inline HTML in your document using <i>$html</i>.
!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text $text This is being written using Wave syntax.<br>$html %<marquee%>Hello from HTML!%</marquee%>
!font Arial
!box 0
!bg bg

$nl 2

!size 25
!color green
$text 3. More Utilities
$nl 3

!size 20
!color green
$text 3.1 Themes

!color fg
$nl 2
$text Wave allows documents to have themes. The colors change their shades according to the selected theme. Set the required theme using the <i>~theme</i> page property.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~theme HorizonDark
!font Arial
!box 0
!bg bg
!color fg

$text Make sure to add the <i>~theme</i> content property at the very top of the document for the theme to properly take effect.
$nl 2

$text You can choose from the following themes:
$nl
$points Default ; OneDark ; Dracula ; Monokai ; GruvboxLight ; GruvboxDark ; SnazzyLight ; LaserWave ; HorizonLight ; HorizonDark ; Nord ; Synthwave84
$nl
$text Theme names are case sensitive.

$nl 3
!color green
$text 3.2 Variables
!color fg

$nl 2

$text Set variables in a Wave script using the <i>~set</i> page property. Variable names cannot have spaces in them.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~set person1 Oliver Queen<br>~set person2 Bruce Wayne
!font Arial
!box 0
!bg bg
!color fg

$text This will set the values of <i>person1</i> and <i>person2</i> to <i>Oliver Queen</i> and <i>Bruce Wayne</i> respectively.
$nl 2

$text Call the variables using the <i>%-</i> symbol. Like,

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~set person1 Oliver Queen<br>~set person2 Bruce Wayne<br>$text %-person1 meets %-person2.
!font Arial
!box 0
!bg bg
!color fg

$text The value of the same variable can be changed multiple times.

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~set Robin Jason Todd<br>~set Robin Tim Drake<br>$text %-Robin is the new Robin.
!font Arial
!box 0
!bg bg
!color fg

$text Use <i>%--</i> to escape the <i>%-</i> symbol. Like,

!box 50
!bg %code_bg
!color %code_fg
!font Consolas
$text ~set name Barry Allen<br>$text %--name
!font Arial
!box 0
!bg bg
!color fg

$text This will render <i>%-name</i> in the document and not <i>Barry Allen</i>.

$nl 3

!color green
$text 3.3 Special Characters
$nl 2
!color fg
$text Wave also supports special HTML characters. Some of the mostly used special characters come as variables in Wave.
$nl
$text Here's the table of some of the special characters available as variables in Wave:
$nl 2

!color brown
!align center
!sep .
$table <b>Variable</b> . <b>Special Character / Value</b> | %-< . %< | %-> . %> | %-& . %& | %-' . %' | %-" . %" | %-- . %- | %-sp . A space character | %-source_name . Name of the Wave file | %-file_name . Name of the transpiled HTML document
!sep ;
!align left
!color fg

$nl 2
$text These special character variables can be overwritten if you use the <i>~set</i> content keyword on them. Therefore, be careful not to override these variables.

$nl
$text HTML tags like <i>%<i%></i>, <i>%<b%></i> and <i>%<br%></i> also work in Wave.
